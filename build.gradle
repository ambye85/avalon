plugins {
    id 'java'
    id 'jacoco'
    id 'application'
}

group 'uk.ashleybye'
version '0.0.1'

sourceCompatibility = JavaVersion.VERSION_14

repositories {
    jcenter()
}

ext {
    lwjglVersion = "3.2.3"
    imguiVersion = "1.76-0.13"
    natives = "natives-macos"
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.fusesource.jansi:jansi:1.18"

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "io.imgui.java:binding:$imguiVersion"
    implementation "io.imgui.java:lwjgl3:$imguiVersion"

    runtimeOnly "org.lwjgl:lwjgl::$natives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$natives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$natives"
    runtimeOnly "io.imgui.java:$natives:$imguiVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"
    testImplementation "com.google.truth:truth:1.0.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.2"
}

application {
    mainClassName = 'uk.ashleybye.juggernaut.Engine'
    applicationDefaultJvmArgs = ['-XstartOnFirstThread', '-XX:+ShowCodeDetailsInExceptionMessages']
}

wrapper {
    gradleVersion = '6.4'
}

jacoco {
    toolVersion = "0.8.5"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run

    jvmArgs '-XX:+ShowCodeDetailsInExceptionMessages'
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}
